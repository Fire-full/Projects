class "ScriptControl";  -- "MSScriptControl.ScriptControl"

inclass

--ПРИМЕР ИСПОЛЬЗОВАНИЯ "VBScript"
--    В Интернете ищем подходящий для "нашей"»задачи скрипт-код. Редактируем его "под себя".
--    Далее..
--
--    VBS = VBScript.Create;                                     --Создаем класс VBScript
--    VBS.Text = 'List = CreateObject("Scripting.Dictionary")';  --Передаём Текст скрипта
--  а далее варианты:
--    VBS.FulFill;                                              -- "FulFill" - выполнить (добавление кода). Если Код все делает сам и ненужно возвращаемое значение.
--  или
--    Имя = VBS.Eval( "List" );                                 -- "List" - переменная в скрипте которая содержит НАШЕ значение
--  или
--    Имя = VBS.Run("Show");                                    -- "Show" - Функция в скрипте которую нужно выполнить и получить результат выполнения Функции
--  или
--    Имя = VBS.Run_Eval("Show", "List");                       -- "Show" - Функция в скрипте которую нужно выполнить
                                                                -- "List" - Нами определенная переменная в скрипте, которая содержит НАШЕ значение
--  и все!!!


var ScrptCtrl : AutoObject;
var Text      : Строка;
--------------------------------------------------------------------------------
функ FulFill : Логическое;   --Выполнить код
  var Рез   : Логическое;
  Рез = Истина;
      Попытка
        ScrptCtrl.addcode( Text );
      Исключение
        ScrptCtrl = NIL;
        Рез       = Ложь;
      end;
  Возврат Рез;
конец;
--------------------------------------------------------------------------------
функ Script_Create : Логическое;
  var Рез   : Логическое;

      Попытка
        ScrptCtrl          =  AutoObject.OpenOrCreate("MSScriptControl.ScriptControl", ИСТИНА);
        ScrptCtrl.Language = "vbscript";
        if FulFill then
          Рез = Истина;
        ELSE
          Рез = Ложь;
        end;
      Исключение
        ScrptCtrl = NIL;
        Рез       = Ложь;
      end;

  Возврат Рез;
конец;
--------------------------------------------------------------------------------
функ Run(Show:Строка) : Вариант;
  var Рез : Вариант;

      Рез     = nil;
      if Script_Create then
        Рез = ScrptCtrl.Run(Show);
      ELSE
        Рез = nil;
      end;

      ScrptCtrl = nil;
      Text      = "" ;

  Возврат Рез;
конец;
--------------------------------------------------------------------------------
функ Eval( ResStr:Строка ) :Вариант;
  var Рез   : Вариант;

      if Script_Create then
        Рез = ScrptCtrl.Eval(ResStr);
      ELSE
        Рез = nil;
      end;

      ScrptCtrl = nil;
      Text      = "" ;

  Возврат Рез;
конец;
--------------------------------------------------------------------------------
функ Run_Eval(Show:Строка; ResStr:Вариант) :Вариант ;
  var Рез      : Вариант     ;

      if Script_Create then
              ScrptCtrl.Run (  Show  );
        Рез = ScrptCtrl.Eval( ResStr );
      ELSE
        Рез = nil;
      end;

      ScrptCtrl = nil;
      Text      = "" ;

  Возврат Рез;
конец;
--------------------------------------------------------------------------------
inobject
--------------------------------------------------------------------------------
end